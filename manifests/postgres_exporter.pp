# Class: prometheus::postgres_exporter
#
# This module manages prometheus postgres_exporter
#
# Parameters:
#  [*arch*]
#  Architecture (amd64 or i386)
#
#  [*bin_dir*]
#  Directory where binaries are located
#
#  [*download_extension*]
#  Extension for the release binary archive
#
#  [*download_url*]
#  Complete URL corresponding to the where the release binary archive can be downloaded
#
#  [*download_url_base*]
#  Base URL for the binary archive
#
#  [*extra_groups*]
#  Extra groups to add the binary user to
#
#  [*extra_options*]
#  Extra options added to the startup command
#
#  [*group*]
#  Group under which the binary is running
#
#  [*init_style*]
#  Service startup scripts style (e.g. rc, upstart or systemd)
#
#  [*install_method*]
#  Installation method: url or package (only url is supported currently)
#
#  [*manage_group*]
#  Whether to create a group for or rely on external code for that
#
#  [*manage_service*]
#  Should puppet manage the service? (default true)
#
#  [*manage_user*]
#  Whether to create user or rely on external code for that
#
#  [*os*]
#  Operating system (linux is the only one supported)
#
#  [*package_ensure*]
#  If package, then use this for package ensure default 'latest'
#
#  [*package_name*]
#  The binary package name - not available yet
#
#  [*purge_config_dir*]
#  Purge config files no longer generated by Puppet
#
#  [*restart_on_change*]
#  Should puppet restart the service on configuration change? (default true)
#
#  [*service_enable*]
#  Whether to enable the service from puppet (default true)
#
#  [*service_ensure*]
#  State ensured for the service (default 'running')
#
#  [*user*]
#  User which runs the service
#
#  [*version*]
#  The binary release version
class prometheus::postgres_exporter (
  String           $postgres_exporter_dsn = $::prometheus::params::postgres_exporter_dsn,
  String           $arch                  = $::prometheus::params::arch,
  String           $bin_dir               = $::prometheus::params::bin_dir,
  Optional[String] $download_url          = undef,
  String           $download_url_base     = $::prometheus::params::postgres_exporter_download_url_base,
  Array            $extra_groups          = $::prometheus::params::postgres_exporter_extra_groups,
  String           $extra_options         = '',
  String           $group                 = $::prometheus::params::postgres_exporter_group,
  String           $init_style            = $::prometheus::params::init_style,
  String           $install_method        = $::prometheus::params::install_method,
  Boolean          $manage_group          = true,
  Boolean          $manage_service        = true,
  Boolean          $manage_user           = true,
  String           $os                    = $::prometheus::params::os,
  String           $package_ensure        = $::prometheus::params::postgres_exporter_package_ensure,
  String           $package_name          = $::prometheus::params::postgres_exporter_package_name,
  Boolean          $purge_config_dir      = true,
  Boolean          $restart_on_change     = true,
  Boolean          $service_enable        = true,
  String           $service_ensure        = 'running',
  String           $user                  = $::prometheus::params::postgres_exporter_user,
  String           $version               = $::prometheus::params::postgres_exporter_version,
) inherits prometheus::params {
  #Please provide the download_url for versions < 0.9.0
  $real_download_url = pick($download_url,"${download_url_base}/download/v${version}/${package_name}")

  $notify_service = $restart_on_change ? {
    true    => Service['postgres_exporter'],
    default => undef,
  }

  $daemon_environment = "DATA_SOURCE_NAME=${postgres_exporter_dsn}"

  # some start styles need a config file
  case $init_style {
    'debian','upstart' : {
      file { '/etc/default/postgres_exporter':
        ensure  => file,
        mode    => '0644',
        content => "export ${daemon_environment}\n",
      }
    }
    'sles', 'sysv': {
      file { '/etc/sysconfig/postgres_exporter':
        ensure  => file,
        mode    => '0644',
        content => "export ${daemon_environment}\n",
      }
    }
    default: {
      notice('using an init style which does not need a config file for postgres_exporter environment')
    }
  }


  prometheus::daemon { 'postgres_exporter':
    install_method     => $install_method,
    version            => $version,
    os                 => $os,
    arch               => $arch,
    real_download_url  => $real_download_url,
    bin_dir            => $bin_dir,
    notify_service     => $notify_service,
    package_name       => $package_name,
    package_ensure     => $package_ensure,
    manage_user        => $manage_user,
    user               => $user,
    extra_groups       => $extra_groups,
    group              => $group,
    manage_group       => $manage_group,
    purge              => $purge_config_dir,
    init_style         => $init_style,
    service_ensure     => $service_ensure,
    service_enable     => $service_enable,
    manage_service     => $manage_service,
    daemon_environment => $daemon_environment,
  }
}
